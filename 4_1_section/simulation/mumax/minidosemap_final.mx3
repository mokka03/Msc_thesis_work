timesteps := 12000
// FixDt = 20.0e-12
dt_ := 20.0e-12
runtime := timesteps * dt_;

Nx := 1200*2
Ny := 1060*2
Nz := 1

//Strip found in the Chumak paper


dx := 50.0E-9
dy := 50.0E-9
dz := 100.0E-9

setgridsize(Nx, Ny, Nz)
setcellsize(dx, dy, dz)


Aex  = 3.65E-12
alpha_ := 18E-4
Msat = 115.65e3
m = uniform(0.0, 0.0, 1.0)

SetPBC(0,1,0)

OutputFormat = OVF2_TEXT

alpha_start :=  alpha_  //default / bulk damping
alpha_end :=  0.5 //maximum damping toward the edge of the structure

dregnum := 100 //number of damping regions per side
dregthickness := 2;  //thickness of damping regions 


for i :=dregnum; i>=0; i--{
  alpha_local := alpha_start + (alpha_end-alpha_start)*pow(i,2)/pow(dregnum,2)
  damprect := rect((Nx-(dregnum-i)*dregthickness*2)*dx, Ny*dy)
  defregion(i,damprect)
  alpha.setregion(i, alpha_local)
}


// saturation magnetization
defregion(255,imageShape("/home/maule/SpinTorch_projects/minidosemap/binary_pictures/20/Msat_level_1.png"))
Msat.setregion(255, 116.72e3)
alpha.setregion(255, alpha_)

defregion(251,imageShape("/home/maule/SpinTorch_projects/minidosemap/binary_pictures/20/Msat_level_2.png"))
Msat.setregion(251, 114.25e3)
alpha.setregion(251, alpha_)

defregion(250,imageShape("/home/maule/SpinTorch_projects/minidosemap/binary_pictures/20/Msat_level_3.png"))
Msat.setregion(250, 112e3)
alpha.setregion(250, alpha_)

save(regions)
save(alpha)
save(Msat)

B_ext = vector(0.0,0.0,0.1986)
relax()

save(m)

source_pos :=  200.0   //Position in relative units

excitation_mask := newVectorMask(Nx, Ny, 1)
for j:=0; j<Ny; j++{
	excitation_mask.setVector(source_pos, j, 0, vector(0.0, 1.0, 0.0))
}

f := 1.6E+9
Amp := 1.0E-3


autosave(m, timesteps*dt_)
// tableautosave(dt_*10)
// autosave(B_ext, 1.0E-9)


B_ext.add(excitation_mask, Amp*sin(2*pi*f*t))

run(runtime)